<!DOCTYPE HTML PUBLIC "-//W3C//DTD HTML 4.0 Transitional//EN">
<!-- saved from url=(0069)https://msdn.microsoft.com/workshop/imedia/agent/TryMicrosoftAgent.htm -->
<HTML><HEAD><TITLE>Try out Microsoft Agent</TITLE>
<META content="text/html; charset=utf-8" http-equiv=Content-Type>
<OBJECT classid=clsid:D45FD31B-5C6E-11D1-9EC1-00C04FD7081F height=1 id=Agent 
style="LEFT: 0px; TOP: 0px" width=1><PARAM NAME="_cx" VALUE="847"><PARAM NAME="_cy" VALUE="847"></OBJECT>
<OBJECT classid=clsid:B8F2846E-CE36-11D0-AC83-00C04FD97575 
codeBase=#VERSION=6,0,0,0 id=TruVoice></OBJECT>
<SCRIPT language=VBScript>

Dim Peedy
Dim LoadRequestUNC
Dim LoadRequestURL
Dim GetShowAnimation

Sub Window_OnLoad
' Load the character
LoadCharacter

End Sub

Sub InitCommands
' Give user access to the Advanced Character Options dialog
Peedy.Commands.RemoveAll
Peedy.Commands.Add "ACO", "Advanced Character Options"

End Sub

Sub DoIntro
InitCommands

' Set the character's language ID
Peedy.LanguageID = &H0409

' Make the character appear
On Error Resume Next
Peedy.MoveTo window.event.screenX-window.event.clientX+400,window.event.screenY-window.event.clientY+50
Peedy.Show

' Start the monologue
Peedy.Speak "Hello!"
Peedy.Play "Blink"
Peedy.Play "Greet"
Peedy.Speak "I am Peedy!"
Peedy.Play "RestPose"
Peedy.Speak "Maybe you've seen my handsome face on TV!"
Peedy.Play "Pleased"
Peedy.Speak "Welcome to the Microsoft Agent Web site."
Peedy.Play "Explain"
Peedy.Speak "Microsoft Agent is a new technology that supports interactive animated characters,"
Peedy.Play "Blink"
Peedy.Speak "providing a foundation for a more natural form of interaction with your computer."
Peedy.Play "Explain"
Peedy.Speak "On this site, you'll be able to find information about Microsoft Agent."
Peedy.Speak "Including the other languages Microsoft Agent is available in,"
Peedy.Speak "And speech engines that you can use with Microsoft Agent."
Peedy.Play "Blink"
Peedy.Speak "You can also find some of my friends on this site."
Peedy.Play "Think"
Peedy.Speak "Peedy, the wizard,"
Peedy.play "Blink"
Peedy.Speak "Genie, the genie,"
Peedy.Play "Explain"
Peedy.Speak "and Robby, the robot."
Peedy.Play "RestPose"
Peedy.Speak "To download them, try our Downloads page."
Peedy.Play "Blink"
Peedy.Speak "You can also find on this site information about how to program characters like me."
Peedy.Speak "We can be programmed to speak using computer-generated voices or with recorded sound files."
Peedy.Speak "We can also be programmed to respond to mouse and keyboard interaction,"
Peedy.Speak "Or even speech input."
Peedy.Speak "If you want further information about how to program a character,"
Peedy.Speak "Please try the Programming Information link."
Peedy.Speak "Or if you just want to try me out,"
Peedy.Play "GestureRight"
Peedy.Speak "Click the buttons on this page."

End Sub


Sub Agent_RequestComplete(ByVal Request)

' The Request to load the ACF character fails 
If Request = LoadRequestURL Then
	
	' If it failed
	If Request.Status = 1 Then
		Msgbox "Peedy could not be loaded. The server may be busy. Try refreshing this page."
		Exit Sub
		
	' If it succeeded
	ElseIf Request.Status = 0 Then
		' Assign Peedy to the loaded character
		Set Peedy = Agent.Characters("Peedy")
		' Get the preliminary animations necessary to show Peedy
		Set GetShowAnimation = Peedy.Get ("state", "showing, speaking")
		Peedy.Get "animation", "Blink, Greet, Pleased, Explain, Think, GestureRight,Idle1_1,Idle2_2, Announce, Uncertain", False
		
	End If

' The request to get the animation 
ElseIf Request = GetShowAnimation Then
	' If it failed 
	If Request.Status = 1 Then
		Msgbox "Request to load the Show animation failed. The server may be busy."
		Exit Sub
	' If it succeeded
	ElseIf Request.Status = 0 Then
		DoIntro
	End If
	
End If

End Sub


Sub LoadCharacter

On Error Resume Next

' Attempt to load the character from the Microsoft Agent Chars directory
Set LoadRequestUNC = Agent.Characters.Load ("Peedy", "Peedy.acs")

' If it fails...
If LoadRequestUNC.Status <> 0 Then
	' Attempt to load the character from the Microsoft Agent site
	Set LoadRequestURL = Agent.Characters.Load ("Peedy", "https://agent.microsoft.com/agent2/chars/Peedy/Peedy.acf")

' It didn't fail so assign Peedy to loaded character file
Else 
	' Assign Peedy to the loaded character
	Set Peedy = Agent.Characters("Peedy")
	DoIntro
	
End If

End Sub


Sub Agent_Click (ByVal CharacterID, ByVal Button, ByVal Shift, ByVal X, ByVal Y)
	Peedy.Stop
	Peedy.Play "Alert"
	Peedy.Speak "Hey, watch it with that pointer!|Oh that tickles!|\pit=50\Hee hee hee!"
	Peedy.Play "Blink"
End Sub

Sub Agent_DragComplete (ByVal CharacterID, ByVal Button, ByVal Shift, ByVal X, ByVal Y)
	Peedy.Stop
	Peedy.Play "Blink"
	Peedy.Speak "Well, \emp\this gives me a new perspective!|I like it much better over here.|Now why did you move me?|Hey, be careful!"
	Peedy.Play "Blink"
End Sub

Sub SpeakButton()

Peedy.Stop

If document.MyForm.text1.value <> "" Then
	Peedy.Speak document.MyForm.text1.value
End If

End Sub


Sub SingButton()

Peedy.Stop
'choose a random response
randomreply = Int((2 - 1 + 1) * Rnd + 1)
    
Select Case randomreply

Case 1
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=60\\pit=120\Dai \pit=100\zee, \pit=80\Dai \pit=60\zee"
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=120\\pit=68\Give \pit=74\me \pit=80\your \pit=68\an, \pit=80\ser \pit=60\true."
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=50\\pit=92\I'm \pit=120\half\pit=100\cray \pit=80\zee,"
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=115\\pit=68\All \pit=74\for \pit=80\the \pit=80\love \pit=100\of \pit=92\you."
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=80\\pit=100\It \pit=112\won't \pit=110\be \pit=92\a \pit=120\sty-\pit=100\lish \pit=92\mare\pit=80\rege."
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=100\\pit=92\I \pit=100\can't \pit=80\a \pit=68\ford, \pit=80\a \pit=68\care- \pit=60\ridge."
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=100\\pit=60\But \pit=80\you'll \pit=100\look \pit=92\sweet,"
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=90\\pit=60\Up \pit=80\on \pit=100\the \pit=92\seat."
        
	Peedy.Speak "\chr="+chr(34)+"monotone"+chr(34)+"\\spd=100\\pit=100\Of \pit=112\a \pit=120\by \pit=100\sic \pit=80\cull \pit=92\built, \pit=60\for \pit=80\two."
        
	Peedy.Play "greet"
	Peedy.Speak "Thank you, thank you, thank you very much."

Case 2
	singingvoice = "\chr="+chr(34)+"monotone"+chr(34)
        
    Peedy.Speak singingvoice+"\\spd=130\\pit=65\Hap-\pit=65\p \pit=73\birth-\pit=65\day \spd=55\\pit=87\to \pit=82\you."

    Peedy.Speak singingvoice+"\\spd=130\\pit=65\Hap-\pit=65\p \pit=73\birth-\pit=65\day \spd=55\\pit=98\to \pit=87\you."
        
    Peedy.Speak singingvoice+"\\spd=130\\pit=65\Hap-\pit=65\p \pit=131\birth-\pit=110\day, \spd=50\\pit=87\from \pit=82\Pee-\pit=73\dee."

    Peedy.Speak singingvoice+"\\spd=130\\pit=117\Hap-\pit=117\p \pit=110\birth-\pit=87\day \spd=55\\pit=98\to \pit=87\you."

    Peedy.Play "restpose"

End Select


End Sub

Sub JokeButton()
'******************************************************
'* This routine tells a joke.                   *
'******************************************************

Peedy.Stop

'choose a random response
randomreply = Int((5 - 1 + 1) * Rnd + 1)
    
Peedy.Speak "You'll \emp\really like this one.|You'll \emp\love this one.|OK, here's a favorite of mine.|Here's a little humor for you.|OK, here's a good one.|You're gonna love this one!"

randomreply=1    
Select Case randomreply
    
Case 1
    Peedy.Play "Announce"
    Peedy.Speak "Have you heard about the PBS virus?"
    Peedy.Play "Suggest"
    Peedy.Speak "Your computer stops in the middle of your program and requests more money."
            
Case 2
    Peedy.Play "Explain"
    Peedy.Speak "Did you hear the one about the guy who poured Cheerios into his PC?"
    Peedy.Play "Congratulate"
    Peedy.Speak "He wanted a cereal interface! Ha ha ha!"
    
Case 3
    Peedy.Play "Explain"
    Peedy.Speak "You know where a bird goes when it loses its tail?"
    Peedy.Play "Announce"
    Peedy.Speak "Why a 'retail' store, of course!"
        
Case 4
     Peedy.Play "Uncertain"
     Peedy.Speak "Know why I always avoid beta software?"
     Peedy.Play "Explain"
     Peedy.Speak "Because 'beta' is Greek for 'still doesn't work'."
    
Case 5
     Peedy.Play "Explain"
     Peedy.Speak "If debugging is the process of removing bugs..."
     Peedy.Play "Confused"
     Peedy.Speak "Is \emp\programming the process of putting in bugs?"
    
End Select
    
Peedy.Play "Pleased"
Peedy.Speak "Oh, I just love \emp\that one.|I'm just a barrel of laffs! Ha! Ha!|I knocked them dead in Vegas with that one.|Sometimes I really crack myself up!|Ha! Ha! I got a million of them!|This joke was generated with 100% recycled electrons."
Peedy.Play "Blink"

End Sub


Sub Agent_Command (ByVal UserInput)
If UserInput.Name = "ACO" Then
     Agent.PropertySheet.Visible = True
End If

End Sub

</SCRIPT>

<FORM name=MyForm>
<META content="html 5.00.2614.3500" name=GENERATOR></HEAD>
<BODY bgColor=#ccddee link=#0000ff text=#000000 vLink=#800080 VALIGN="0">
<P><STRONG><FONT face="" size=6>Try Out Microsoft Agent</FONT></STRONG></P>
<P>Type in something and click the Speak button.</P>
<P></INPUT><INPUT id=text1 name=text1 style="HEIGHT: 22px; WIDTH: 276px">&nbsp; <INPUT id=button6 name=button6 onclick=SpeakButton() style="LEFT: 197px; TOP: 246px" type=button value=Speak></P>
<P>Or click one of these other buttons:</P>
<P><STRONG></STRONG><INPUT id=button7 name=button7 onclick=JokeButton() style="HEIGHT: 24px; WIDTH: 117px" type=button value="Tell a Joke">&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp; 
<INPUT id=button7 name=button7 onclick=SingButton() style="HEIGHT: 24px; WIDTH: 117px" type=button value="Sing a Song">&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;&nbsp;</P>
<P><B>Here's what you'll need to use this page: </B>
<OL>
  <LI>WindowsВ® 95, Windows 98, or Windows NT 4.0 (x86) 
  <LI>Internet Explorer version 4.0 or later 
  <LI>A Pentium 100 MHz PC (or faster) 
  <LI>At least 16 MB of RAM 
  <LI>At least 661K free disk space for the Microsoft Agent components, 3.1 MB 
  for Peedy and an additional 1.34 MB for the L&amp;H TruVoice Text-To-Speech 
  engine 
  <LI>A Windows-compatible sound card 
  <LI>A compatible set of speakers and microphone (recommended) </LI></OL>
<P><STRONG>To access other Agent components, go to the&nbsp; </STRONG><A 
href="https://msdn.microsoft.com/workshop/imedia/agent/agentdl.asp">Downloads
page</A>.</P>
<P><STRONG>To learn how to program the character to do other things, go to 
the</STRONG> <A 
href="https://msdn.microsoft.com/workshop/imedia/agent/documentation.asp">Microsoft
Agent Documentation page</A>.</P></FORM></BODY></HTML>
